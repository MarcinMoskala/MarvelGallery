apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.3"
    defaultConfig {
        applicationId "com.naxtlevelofandroiddevelopment.marvelgallery"
        minSdkVersion 15
        targetSdkVersion 25
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        if (System.getenv().containsKey("IS_CI_JOB")) {
            buildConfigField("String", "PUBLIC_KEY", "\"" + System.getenv("CI_PUBLIC_KEY") + "\"")
            buildConfigField("String", "PRIVATE_KEY", "\"" + System.getenv("CI_PRIVATE_KEY") + "\"")
        } else {
            buildConfigField("String", "PUBLIC_KEY", "\"${marvelPublicKey}\"")
            buildConfigField("String", "PRIVATE_KEY", "\"${marvelPrivateKey}\"")
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"

    compile "com.android.support:appcompat-v7:$android_support_version"
    compile "com.android.support:design:$android_support_version"
    compile "com.android.support:support-v4:$android_support_version"
    compile "com.android.support:recyclerview-v7:$android_support_version"

    // PaperParcel - used to simplify making objects parcelable
    compile(["com.github.grandstaish.paperparcel:paperparcel:$paperparcel_version",
             "com.github.grandstaish.paperparcel:paperparcel-kotlin:$paperparcel_version"])
    kapt "com.github.grandstaish.paperparcel:compiler:$paperparcel_version"

    // Glide - used for image loading
    compile "com.github.bumptech.glide:glide:$glide_version"

    // RxJava
    compile "io.reactivex.rxjava2:rxjava:$rxjava_version"

    // RxAndroid
    compile "io.reactivex.rxjava2:rxandroid:$rxandroid_version"

    // Retrofit
    compile(["com.squareup.retrofit2:retrofit:$retrofit_version",
             "com.squareup.retrofit2:adapter-rxjava2:$retrofit_version",
             "com.squareup.retrofit2:converter-gson:$retrofit_version",
             "com.squareup.okhttp3:okhttp:$okhttp_version",
             "com.squareup.okhttp3:logging-interceptor:$okhttp_version"])

    // Espresso
    androidTestCompile("com.android.support.test.espresso:espresso-core:$espresso_version", {
        exclude group: 'com.android.support', module: 'support-annotations'
    })

    // JUnit
    testCompile "junit:junit:$junit_version"
}
repositories {
    mavenCentral()
    maven { url 'https://jitpack.io' }
}
